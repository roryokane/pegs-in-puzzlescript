(The game Pegs for the TI-83+ graphing calculator, ported to the PuzzleScript engine.)
(The original Pegs was part of PuzzPack by Detached Solutions, and its main developer was Fred Coughlin. This port is by Rory O’Kane.)

title Pegs
author Fred Coughlin / Rory O'Kane
homepage http://roryokane.com/
(homepage of the original Pegs is http://www.detachedsolutions.com/puzzpack/pegs.php)

background_color LightGray
text_color Black
key_repeat_interval 0.25 (to prevent accidental falling down pits, etc.)
run_rules_on_level_start (to draw hole boundaries)

========
OBJECTS
========

(some colors chosen using HUSL: http://boronine.com/husl/)

Background
White

Wall
#111 #222 #333 #eee
00000
01310
03230
01310
00000

NormalPlayer
(Orange = #eb8931)
(HUSL Lightnesses 66, 63, 58, 54)
#eb8931 #db7f2d #cd762a #be6d26
.010.
0.2.0
12321
0.2.0
.010.

SelectingPlayer
(HUSL Lightnesses 88, 84, 80, 76)
#dddddd #d1d1d1 #c6c6c6 #bbbbbb
.010.
0.2.0
12321
0.2.0
.010.

(peg colors are HUSL pastel Saturation 100%, Lightness 25% and 85%)
(insides of selected pegs are 50% Saturation)

Circle
#3b3d1c #d4d8a8 (hue 90, yellow)
.000.
01110
01110
01110
.000.

SelectedCircle
#db7f2d #d4d6bf
.000.
01110
01110
01110
.000.

Square
#1a404e #acdbf3 (hue 225, blue)
00000
01110
01110
01110
00000

SelectedSquare
#db7f2d #c2d8e4
00000
01110
01110
01110
00000

Triangle
#1b432e #a8e1c0 (hue 145, green)
0....
00...
010..
0110.
00000

SelectedTriangle
#db7f2d #bfdbca
0....
00...
010..
0110.
00000

Cross
#3b3b3b
..0..
..0..
00000
..0..
..0..

SelectedCross
#db7f2d
..0..
..0..
00000
..0..
..0..

Hole
Black

HoleTopBoundary
#222
00000
.....
.....
.....
.....

HoleBottomBoundary
#222
.....
.....
.....
.....
00000

HoleLeftBoundary
#222
0....
0....
0....
0....
0....

HoleRightBoundary
#222
....0
....0
....0
....0
....0

RestartReadyDeadPlayer
White
.....
.....
.....
.....
.....

=======
LEGEND
=======

Player = NormalPlayer or SelectingPlayer or RestartReadyDeadPlayer
SelectedBlock = SelectedCircle or SelectedSquare or SelectedTriangle or SelectedCross
Block = Circle or Square or Triangle or Cross or SelectedBlock
HoleBoundary = HoleTopBoundary or HoleBottomBoundary or HoleLeftBoundary or HoleRightBoundary

. = Background
· = Background
p = NormalPlayer
P = NormalPlayer
✠ = NormalPlayer
X = Wall
⛝ = Wall
# = Hole
■ = Hole
o = Circle
○ = Circle
s = Square
□ = Square
\ = Triangle
< = Triangle
◺ = Triangle
+ = Cross
✚ = Cross


=======
SOUNDS
=======
sfx0 9351102 (fill hole with square block)
sfx1 60846506 (cycle selected block)
sfx2 57903306 (confirm selection)
sfx3 29154902 (destroy through combining)
sfx4 39221701 (push block down hole)
sfx5 93422102 (combine triangles, forming a wall)
sfx9 36821508 (combined blocks don't match)
sfx10 3824708 (player falls in hole)
Block move 16776907
undo 418707
startlevel 27686900
restart 73263508
endlevel 2807108

================
COLLISIONLAYERS
================

Background
Player Wall, Hole, Block, SelectedBlock
HoleTopBoundary
HoleBottomBoundary
HoleLeftBoundary
HoleRightBoundary

======
RULES
======

(allow scrolling through selections without moving player)
DOWN [ > SelectingPlayer ] [ SelectedTriangle ] -> [ SelectingPlayer ] [ SelectedCross ] sfx1
(simpler rules like this don't work for some reason: DOWN [ SelectedTriangle ] -> [ SelectedCross ])
DOWN [ > SelectingPlayer ] [ SelectedCross ] -> [ SelectingPlayer ] [ SelectedSquare ] sfx1
DOWN [ > SelectingPlayer ] [ SelectedSquare ] -> [ SelectingPlayer ] [ SelectedCircle ] sfx1
DOWN [ > SelectingPlayer ] [ SelectedCircle ] -> [ SelectingPlayer ] [ SelectedTriangle ] sfx1
UP [ > SelectingPlayer ] [ SelectedTriangle ] -> [ SelectingPlayer ] [ SelectedCircle ] sfx1
UP [ > SelectingPlayer ] [ SelectedCircle ] -> [ SelectingPlayer ] [ SelectedSquare ] sfx1
UP [ > SelectingPlayer ] [ SelectedSquare ] -> [ SelectingPlayer ] [ SelectedCross ] sfx1
UP [ > SelectingPlayer ] [ SelectedCross ] -> [ SelectingPlayer ] [ SelectedTriangle ] sfx1
[ > SelectingPlayer ] -> [ SelectingPlayer ]

(you can't move when you're dead)
[ > RestartReadyDeadPlayer ] -> [ RestartReadyDeadPlayer ]
[ Action RestartReadyDeadPlayer ] -> restart

(action confirms selection)
[ Action SelectingPlayer ] [ SelectedCircle ] -> [ NormalPlayer ] [ Circle ] sfx2
[ Action SelectingPlayer ] [ SelectedSquare ] -> [ NormalPlayer ] [ Square ] sfx2
[ Action SelectingPlayer ] [ SelectedTriangle ] -> [ NormalPlayer ] [ Triangle ] sfx2
[ Action SelectingPlayer ] [ SelectedCross ] -> [ NormalPlayer ] [ Cross ] sfx2

(player can push blocks)
[ > Player | Block ] -> [ > Player | > Block ]

(square pegs fill holes; other pegs fall in and disappear)
[ > Square | Hole ] -> [ | ] sfx0
[ > Block | Hole ] -> [ | Hole ] sfx4

(when you fall down a hole, you must reset or undo)
[ > Player | Hole ] -> [ RestartReadyDeadPlayer | Hole ] sfx10 message You fell and died.

(draw hole boundaries)
UP [ Hole no HoleTopBoundary | no Hole ] -> [ Hole HoleTopBoundary | no Hole ]
DOWN [ Hole no HoleBottomBoundary | no Hole ] -> [ Hole HoleBottomBoundary | no Hole ]
LEFT [ Hole no HoleLeftBoundary | no Hole ] -> [ Hole HoleLeftBoundary | no Hole ]
RIGHT [ Hole no HoleRightBoundary | no Hole ] -> [ Hole HoleRightBoundary | no Hole ]
[ HoleBoundary no Hole ] -> [ ]

(some pegs disappear when combined)
[ > Circle | Circle ] -> [ | ] sfx3
[ > Square | Square ] -> [ | ] sfx3
(combined triangles turn into a wall)
[ > Triangle | Triangle ] -> [ | Wall ] sfx5
(combined crosses let you freely select a replacement)
[ > Cross | Cross ] [ > Player ] -> [ | SelectedTriangle ] [ > SelectingPlayer ] sfx3

(player gets scolded if non-matching pegs are combined)
[ > Circle | Block no Circle ] -> [ Circle | Block ] sfx9 message They don't match.
[ > Triangle | Block no Triangle ] -> [ Triangle | Block ] sfx9 message They don't match.
[ > Square | Block no Square ] -> [ Square | Block ] sfx9 message They don't match.
[ > Cross | Block no Cross ] -> [ Cross | Block ] sfx9 message They don't match.

==============
WINCONDITIONS
==============

No Block

=======
LEVELS
=======

(help)
(message To beat each level you must
message destroy all of the pegs.
message If two like pegs are
message pushed into each other
message they disappear,
message except for triangles
message which form a solid block
message and plusses which allow you to
message choose a replacement block.)

XXXXXXXXXXXX
X··········X
X···P··o···X
X····++<···X
X···□■·o···X
X··········X
X··········X
XXXXXXXXXXXX

message Nice Pegging!!!

XXXXXXXXXXXX
XXX···<<···X
XXX···X····X
XXX···XX···X
P···<<oo···X
XXX···XX···X
XXX···X····X
XXX···<<···X

message Nice Pegging!!!

XXXXXXXXXXXX
XXXXXXXXXXXX
X·····<·P···
X·XX·XX·XX<X
X··<·······X
XXXXXXXXXX<X
XXXXXXXXXXXX
XXXXXXXXXXXX

message Nice Pegging!!!

XXXXXXXXXXXX
■·□·<·<····X
■·+·o·<····X
■·o·□P<····X
■·<·□·<·+··X
■·□·o·<····X
■·o·□·<····X
XXXXXXXXXXXX

message Nice Pegging!!!

·■■·□■··■···
·■■·□□··■··<
<■■·■■■·X<<<
·■■·■□■·X<·<
·■■·■P■o■···
<■■·■■■·■···
·■■·□···■···
·■■··o··■oo·

message Nice Pegging!!!

P···········
<·········+·
<··<···<<···
·XX<XXXXXXX·
············
·XXXX·XX·XXX
·+······o□■·
···XXXX··XXX

message Nice Pegging!!!

XXXX··XXXXXX
XXX■··□■·XXX
XXXX·XoX·XXX
XXX<++P□<■XX
XXXX·X<X···X
XXXX··o··X·X
XXXXXXX·XX·X
XXXXXXX····X

message Nice Pegging!!!

XXXXXXXXXXXX
··+++++□++··
··+++<++o+··
P·+++<++□+··
··++□o++++··
··++o<++++··
··++<□++++··
XXXXXXXXXXXX

message Nice Pegging!!!

····XX······
·+··XX······
····<□□+·XX·
·X··<X···XXX
···<<PX····<
····<X··XXXX
·+X·<oo···+·
····□X······

message Nice Pegging!!!

XXXX<XXX····
XXXX·XXX···P
··□·■■■···+·
·++·■■■·□+□·
·++·■■■·□+□·
····■■■·+···
XXXXX·XX····
XXXXX<XX····

message Nice Pegging!!!

XPX·XX■··XXX
X···o□■■·<<·
X<X<·X■··<<·
X··<·X···++·
X<X··X·X·++·
·····X·XX·X·
·+□+·X·X····
····<<<XXXXX

message Nice Pegging!!!

P·····XXXXXX
X<X<X<XX···X
······X··X·X
X<X<X<X····X
······XXoo■X
X<X<X<XX·<<X
··········<X
X·XXXXXX··XX

message Nice Pegging!!!

XXXXXXXXXXXX
··□·<·o·□··■
··<·o·<·□··■
P·+·□·o·□··■
··o·□·<·□··■
··□·<·o·□··■
··+·+·<·□··■
XXXXXXXXXXXX

message Nice Pegging!!!

XXXXX··X··XX
XX·<··□··<·X
X■··oX·XX··X
XXX··+o+··XX
XX··XX·Xo··■
XX·<··□···PX
XX··X··XXXXX
XXXXXXXXXXXX

message Nice Pegging!!!

···■·■·■·■·X
···■·■·■·■·X
·+□■·■·■·■·<
·□·■·■□■·■·X
·+·■·■·■·■·X
P··■□■·■·■·X
··□■·■·■□■·X
··□■·■·■·■·X

message Congratulations!
message You finished Pegs!
