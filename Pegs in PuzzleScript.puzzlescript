(The game Pegs for the TI-83+ graphing calculator, ported to the PuzzleScript engine.)
(The original Pegs was part of PuzzPack by Detached Solutions, and its main developer was Fred Coughlin. This port is by Rory O’Kane.)

title Pegs
author Fred Coughlin / Rory O'Kane
homepage http://roryokane.com/
(homepage of the original Pegs is http://www.detachedsolutions.com/puzzpack/pegs.php)
background_color LightGray
text_color Black

========
OBJECTS
========

Background
White

Wall
Black
00000
00.00
0.0.0
00.00
00000

NormalPlayer
Orange
.000.
0.0.0
00000
0.0.0
.000.

SelectingPlayer
LightGray
.000.
0.0.0
00000
0.0.0
.000.

Circle
DarkGray
.000.
0...0
0...0
0...0
.000.

SelectedCircle
Orange
.000.
0...0
0...0
0...0
.000.

Square
DarkGray
00000
0...0
0...0
0...0
00000

SelectedSquare
Orange
00000
0...0
0...0
0...0
00000

Triangle
DarkGray
0....
00...
0.0..
0..0.
00000

SelectedTriangle
Orange
0....
00...
0.0..
0..0.
00000

Cross
DarkGray
..0..
..0..
00000
..0..
..0..

SelectedCross
Orange
..0..
..0..
00000
..0..
..0..

Hole
Black

=======
LEGEND
=======

Player = NormalPlayer or SelectingPlayer
SelectedBlock = SelectedCircle or SelectedSquare or SelectedTriangle or SelectedCross
Block = Circle or Square or Triangle or Cross or SelectedBlock
NonCircleBlock = Square or Triangle or Cross or SelectedBlock
NonSquareBlock = Circle or Triangle or Cross or SelectedBlock
NonTriangleBlock = Circle or Square or Cross or SelectedBlock
NonCrossBlock = Circle or Square or Triangle or SelectedBlock

. = Background
· = Background
p = NormalPlayer
P = NormalPlayer
✠ = NormalPlayer
X = Wall
⛝ = Wall
# = Hole
■ = Hole
o = Circle
○ = Circle
s = Square
□ = Square
\ = Triangle
< = Triangle
◺ = Triangle
+ = Cross
✚ = Cross


=======
SOUNDS
=======
sfx0 9351102 (fill hole)
sfx1 60846506 (cycle selected block)
sfx2 57903306 (confirm selection)
sfx3 29154902 (destroy through combining)
sfx4 94264508 (push block down hole) (would prefer sweeping, like 71860308 but less whiny)
sfx5 93422102 (combine triangles, forming a wall)
sfx9 36821508 (combined blocks don't match)
sfx10 3824708 (player falls in hole)
Block move 40186307
startlevel 27686900
endlevel 2807108

================
COLLISIONLAYERS
================

Background
NormalPlayer, Wall, Hole, Block
SelectingPlayer, SelectedBlock (these never move, so collisions are irrelevant)

======
RULES
======

(allow scrolling through selections without moving player)
DOWN [ > SelectingPlayer ] [ SelectedTriangle ] -> [ SelectingPlayer ] [ SelectedCross ] sfx1
(simpler rules like this don't work for some reason: DOWN [ SelectedTriangle ] -> [ SelectedCross ])
DOWN [ > SelectingPlayer ] [ SelectedCross ] -> [ SelectingPlayer ] [ SelectedSquare ] sfx1
DOWN [ > SelectingPlayer ] [ SelectedSquare ] -> [ SelectingPlayer ] [ SelectedCircle ] sfx1
DOWN [ > SelectingPlayer ] [ SelectedCircle ] -> [ SelectingPlayer ] [ SelectedTriangle ] sfx1
UP [ > SelectingPlayer ] [ SelectedTriangle ] -> [ SelectingPlayer ] [ SelectedCircle ] sfx1
UP [ > SelectingPlayer ] [ SelectedCircle ] -> [ SelectingPlayer ] [ SelectedSquare ] sfx1
UP [ > SelectingPlayer ] [ SelectedSquare ] -> [ SelectingPlayer ] [ SelectedCross ] sfx1
UP [ > SelectingPlayer ] [ SelectedCross ] -> [ SelectingPlayer ] [ SelectedTriangle ] sfx1
[ > SelectingPlayer ] -> [ SelectingPlayer ]

(action confirms selection)
[ Action SelectingPlayer ] [ SelectedCircle ] -> [ NormalPlayer ] [ Circle ] sfx2
[ Action SelectingPlayer ] [ SelectedSquare ] -> [ NormalPlayer ] [ Square ] sfx2
[ Action SelectingPlayer ] [ SelectedTriangle ] -> [ NormalPlayer ] [ Triangle ] sfx2
[ Action SelectingPlayer ] [ SelectedCross ] -> [ NormalPlayer ] [ Cross ] sfx2

(player can push blocks)
[ > Player | Block ] -> [ > Player | > Block ]

(square pegs fill holes; other pegs fall in and disappear)
[ > Square | Hole ] -> [ | ] sfx0
[ > Block | Hole ] -> [ | Hole ] sfx4

[ > Player | Hole ] -> [ | Hole ] sfx10 message You fell and died.

(some pegs disappear when combined)
[ > Circle | Circle ] -> [ | ] sfx3
[ > Square | Square ] -> [ | ] sfx3
(combined triangles turn into a wall)
[ > Triangle | Triangle ] -> [ | Wall ] sfx5
(combined crosses let you freely select a replacement)
[ > Cross | Cross ] [ > Player ] -> [ | SelectedTriangle ] [ > SelectingPlayer ] sfx3

(player get scolded if non-matching pegs are combined)
[ > Circle | NonCircleBlock ] -> [ Circle | NonCircleBlock ] sfx9 message They don't match.
[ > Triangle | NonTriangleBlock ] -> [ Triangle | NonTriangleBlock ] sfx9 message They don't match.
[ > Square | NonSquareBlock ] -> [ Square | NonSquareBlock ] sfx9 message They don't match.
[ > Cross | NonCrossBlock ] -> [ Cross | NonCrossBlock ] sfx9 message They don't match.

==============
WINCONDITIONS
==============

No Block

=======
LEVELS
=======

XXXXXXXXXXXX
X··········X
X···P··o···X
X····++<···X
X···□■·o···X
X··········X
XXXXXXXXXXXX

message Nice Pegging!!!

XXXXXXXXXXXX
XXX···<<···X
XXX···X····X
XXX···XX···X
P···<<oo···X
XXX···XX···X
XXX···X····X
XXX···<<···X

message Nice Pegging!!!

XXXXXXXXXXXX
XXXXXXXXXXXX
X·····<·P···
X·XX·XX·XX<X
X··<·······X
XXXXXXXXXX<X
XXXXXXXXXXXX
XXXXXXXXXXXX

message Nice Pegging!!!

XXXXXXXXXXXX
■·□·<·<····X
■·+·o·<····X
■·o·□P<····X
■·<·□·<·+··X
■·□·o·<····X
■·o·□·<····X
XXXXXXXXXXXX

message Nice Pegging!!!

·■■·□■··■···
·■■·□□··■··<
<■■·■■■·X<<<
·■■·■□■·X<·<
·■■·■P■o■···
<■■·■■■·■···
·■■·□···■···

message Nice Pegging!!!

P···········
<·········+·
<··<···<<···
·XX<XXXXXXX·
············
·XXXX·XX·XXX
·+······o□■·
···XXXX··XXX

message Nice Pegging!!!

XXXX··XXXXXX
XXX■··□■·XXX
XXXX·XoX·XXX
XXX<++P□<■XX
XXXX·X<X···X
XXXX··o··X·X
XXXXXXX·XX·X
XXXXXXX····X

message Nice Pegging!!!

XXXXXXXXXXXX
··+++++□++··
··+++<++o+··
P·+++<++□+··
··++□o++++··
··++o<++++··
··++<□++++··
XXXXXXXXXXXX

message Nice Pegging!!!

····XX······
·+··XX······
····<□□+·XX·
·X··<X···XXX
···<<PX····<
····<X··XXXX
·+X·<oo···+·
····□X······

message Nice Pegging!!!

XXXX<XXX····
XXXX·XXX···P
··□·■■■···+·
·++·■■■·□+□·
·++·■■■·□+□·
····■■■·+···
XXXXX·XX····
XXXXX<XX····

message Nice Pegging!!!

XPX·XX■··XXX
X···o□■■·<<·
X<X<·X■··<<·
X··<·X···++·
X<X··X·X·++·
·····X·XX·X·
·+□+·X·X····
····<<<XXXXX

message Nice Pegging!!!

P·····XXXXXX
X<X<X<XX···X
······X··X·X
X<X<X<X····X
······XXoo■X
X<X<X<XX·<<X
··········<X
X·XXXXXX··XX

message Nice Pegging!!!

XXXXXXXXXXXX
··□·<·o·□··■
··<·o·<·□··■
P·+·□·o·□··■
··o·□·<·□··■
··□·<·o·□··■
··+·+·<·□··■
XXXXXXXXXXXX

message Nice Pegging!!!

XXXXX··X··XX
XX·<··□··<·X
X■··oX·XX··X
XXX··+o+··XX
XX··XX·Xo··■
XX·<··□···PX
XX··X··XXXXX
XXXXXXXXXXXX

message Nice Pegging!!!

···■·■·■·■·X
···■·■·■·■·X
·+□■·■·■·■·<
·□·■·■□■·■·X
·+·■·■·■·■·X
P··■□■·■·■·X
··□■·■·■□■·X
··□■·■·■·■·X

message Congratulations!
message You finished Pegs!
