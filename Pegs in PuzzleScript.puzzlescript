title Pegs
author Fred Coughlin / Rory O'Kane
homepage http://www.detachedsolutions.com/puzzpack/pegs.php

========
OBJECTS
========

Background
White

Wall
Black
00000
00.00
0.0.0
00.00
00000

NormalPlayer
Blue
.000.
0.0.0
00000
0.0.0
.000.

SelectingPlayer
Gray
.000.
0.0.0
00000
0.0.0
.000.

Circle
Black
.000.
0...0
0...0
0...0
.000.

SelectedCircle
Blue
.000.
0...0
0...0
0...0
.000.

Square
Black
00000
0...0
0...0
0...0
00000

SelectedSquare
Blue
00000
0...0
0...0
0...0
00000

Triangle
Black
0....
00...
0.0..
0..0.
00000

SelectedTriangle
Blue
0....
00...
0.0..
0..0.
00000

Cross
Black
..0..
..0..
00000
..0..
..0..

SelectedCross
Blue
..0..
..0..
00000
..0..
..0..

Hole
Black

=======
LEGEND
=======

Player = NormalPlayer or SelectingPlayer
SelectedBlock = SelectedCircle or SelectedSquare or SelectedTriangle or SelectedCross
Block = Circle or Square or Triangle or Cross or SelectedBlock
NonCircleBlock = Square or Triangle or Cross or SelectedBlock
NonSquareBlock = Circle or Triangle or Cross or SelectedBlock
NonTriangleBlock = Circle or Square or Cross or SelectedBlock
NonCrossBlock = Circle or Square or Triangle or SelectedBlock
. = Background
p = NormalPlayer
X = Wall
# = Hole
O = Circle
s = Square
\ = Triangle
+ = Cross


=======
SOUNDS
=======
sfx0 9351102 (fill hole) (old: 20917302)
sfx1 60846506 (cycle selected block)
sfx2 57903306 (confirm selection)
sfx3 29154902 (destroy through combining)
sfx4 94264508 (push block down hole) (want sweeping like 71860308 but less whiny)
sfx9 36821508 (combined blocks don't match) (or maybe 23220306, which is less punishing)
sfx10 3824708 (player falls in hole)
startlevel 27686900
endlevel 2807108

================
COLLISIONLAYERS
================

Background
NormalPlayer, Wall, Hole, Block
SelectingPlayer, SelectedBlock (they never move, so collisions are irrelevant)

======
RULES
======

(allow scrolling through selections without moving player)
DOWN [ > SelectingPlayer ] [ SelectedTriangle ] -> [ SelectingPlayer ] [ SelectedCross ] sfx1
(simpler rules like this don't work for some reason: DOWN [ SelectedTriangle ] -> [ SelectedCross ])
DOWN [ > SelectingPlayer ] [ SelectedCross ] -> [ SelectingPlayer ] [ SelectedSquare ] sfx1
DOWN [ > SelectingPlayer ] [ SelectedSquare ] -> [ SelectingPlayer ] [ SelectedCircle ] sfx1
DOWN [ > SelectingPlayer ] [ SelectedCircle ] -> [ SelectingPlayer ] [ SelectedTriangle ] sfx1
UP [ > SelectingPlayer ] [ SelectedTriangle ] -> [ SelectingPlayer ] [ SelectedCircle ] sfx1
UP [ > SelectingPlayer ] [ SelectedCircle ] -> [ SelectingPlayer ] [ SelectedSquare ] sfx1
UP [ > SelectingPlayer ] [ SelectedSquare ] -> [ SelectingPlayer ] [ SelectedCross ] sfx1
UP [ > SelectingPlayer ] [ SelectedCross ] -> [ SelectingPlayer ] [ SelectedTriangle ] sfx1
[ > SelectingPlayer ] -> [ SelectingPlayer ]

(action confirms selection)
[ Action SelectingPlayer ] [ SelectedCircle ] -> [ NormalPlayer ] [ Circle ] sfx2
[ Action SelectingPlayer ] [ SelectedSquare ] -> [ NormalPlayer ] [ Square ] sfx2
[ Action SelectingPlayer ] [ SelectedTriangle ] -> [ NormalPlayer ] [ Triangle ] sfx2
[ Action SelectingPlayer ] [ SelectedCross ] -> [ NormalPlayer ] [ Cross ] sfx2

(player can push blocks)
[ > Player | Block ] -> [ > Player | > Block ]

(square pegs fill holes; other pegs fall in and disappear)
[ > Square | Hole ] -> [ | ] sfx0
[ > Block | Hole ] -> [ | Hole ] sfx4

[ > Player | Hole ] -> [ | Hole ] sfx10 message You fell and died.

(most like pegs disappear when combined)
[ > Circle | Circle ] -> [ | ] sfx3
[ > Triangle | Triangle ] -> [ | ] sfx3
[ > Square | Square ] -> [ | ] sfx3

(combined crosses let you freely select a replacement)
[ > Cross | Cross ] [ > Player ] -> [ | SelectedTriangle ] [ > SelectingPlayer ] sfx3

(player get scolded if non-matching pegs are combined)
[ > Circle | NonCircleBlock ] -> [ Circle | NonCircleBlock ] sfx9 message They don't match.
[ > Triangle | NonTriangleBlock ] -> [ Triangle | NonTriangleBlock ] sfx9 message They don't match.
[ > Square | NonSquareBlock ] -> [ Square | NonSquareBlock ] sfx9 message They don't match.
[ > Cross | NonCrossBlock ] -> [ Cross | NonCrossBlock ] sfx9 message They don't match.

==============
WINCONDITIONS
==============

No Block

=======
LEVELS
=======

XXXXXXXXXXXX
X..........X
X...p..O...X
X....++\...X
X...s#.O...X
X..........X
XXXXXXXXXXXX

message Nice Pegging!!!
